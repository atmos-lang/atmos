require "atmos"
val x = require "atmos.x"
require "atmos.env.pico"
val pico = require "pico"

pico[:set].title "Atmos-SDL: Click, Drag, Cancel"
pico[:set].size.window(256, 256)
pico[:set].font(nil, 20)

var text = ""
var rect = @{x=256/2,y=256/2, w=40,h=40}

spawn {
    val pt = @{x=256/2, y=220}
    every :draw {
        pico.output.draw.rect(rect)
        pico.output.draw.text(pt, text)
    }
}

loop {
    val click = await(:mouse.button.dn, \{pico.vs.pos_rect(it, rect)})
    val orig = x.copy(rect)
    set text = "... clicking ..."
    par_or {
        await(:key.dn, :Escape)
        set rect = orig
        set text = "!!! CANCELLED !!!"
    } with {
        par_or {
            await :mouse.motion
            set text = "... dragging ..."
            await :mouse.button.up
            set text = "!!! DRAGGED !!!"
        } with {
            every e in :mouse.motion {
                set rect.x = orig.x + (e.x - click.x)
                set rect.y = orig.y + (e.y - click.y)
            }
        }
    } with {
        await :mouse.button.up
        set text = "!!! CLICKED !!!"
    }
}
